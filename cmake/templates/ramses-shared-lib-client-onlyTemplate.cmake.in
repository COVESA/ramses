#  -------------------------------------------------------------------------
#  Copyright (C) 2018 BMW Car IT GmbH
#  -------------------------------------------------------------------------
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at https://mozilla.org/MPL/2.0/.
#  -------------------------------------------------------------------------

# Use like this with find_package:
# find_package(ramses-shared-lib-client-only <RAMSES-VERSION> [REQUIRED])

INCLUDE(FindPackageHandleStandardArgs)
INCLUDE(CMakeFindDependencyMacro)
SET(ramses-shared-lib-client-only_VERSION @RAMSES_VERSION@)

@PACKAGE_INIT@


#find include dir
FIND_PATH(ramses-shared-lib-client-only_INCLUDE_DIRS ramses-client.h
    HINTS @PACKAGE_RAMSES_INSTALL_HEADERS_PATH@
)

if(IS_DIRECTORY "${ramses-shared-lib-client-only_INCLUDE_DIRS}/glm")
    add_library(glm::glm INTERFACE IMPORTED)
    set_target_properties(glm::glm PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${ramses-shared-lib-client-only_INCLUDE_DIRS})
else()
    find_dependency(glm REQUIRED)

    if(NOT TARGET glm::glm)
        add_library(glm::glm ALIAS glm)
    endif()
endif()


IF (CMAKE_SYSTEM_NAME MATCHES "Windows")

    # no shlib versioning support on windows
    FIND_LIBRARY(ramses-shared-lib-client-only_LIBRARIES
        NAMES "ramses-shared-lib-client-only"
        HINTS @PACKAGE_RAMSES_INSTALL_RUNTIME_PATH@
    )

ELSEIF (CMAKE_SYSTEM_NAME MATCHES "Linux")

    # require exact shlib version
    FIND_LIBRARY(ramses-shared-lib-client-only_LIBRARIES
        NAMES "libramses-shared-lib-client-only.so.@RAMSES_VERSION_MAJOR@.@RAMSES_VERSION_MINOR@"
        HINTS @PACKAGE_RAMSES_INSTALL_LIBRARY_PATH@
    )

ELSE()
    MESSAGE(FATAL_ERROR "Can't detect system type from within CMake. Expect further issues!")
ENDIF()


FIND_PACKAGE_HANDLE_STANDARD_ARGS(ramses-shared-lib-client-only
    FOUND_VAR ramses-shared-lib-client-only_FOUND
    REQUIRED_VARS ramses-shared-lib-client-only_LIBRARIES ramses-shared-lib-client-only_INCLUDE_DIRS
    VERSION_VAR ramses-shared-lib-client-only_VERSION)

set(ramses-sdk_VERSION "@ramses-sdk_VERSION@" CACHE STRING "Ramses version" FORCE)

message(STATUS "Found ramses-shared-lib-client-only libs: ${ramses-shared-lib-client-only_LIBRARIES}")
message(STATUS "Found ramses-shared-lib-client-only includes: ${ramses-shared-lib-client-only_INCLUDE_DIRS}")

MARK_AS_ADVANCED(
    ramses-shared-lib-client-only_INCLUDE_DIRS
    ramses-shared-lib-client-only_LIBRARIES
    ramses-shared-lib-client-only_FOUND
    )

if (${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.13")
    if (NOT TARGET ramses-ramses-shared-lib-client-only)
        add_library(ramses-ramses-shared-lib-client-only SHARED IMPORTED GLOBAL)
        set_target_properties(ramses-ramses-shared-lib-client-only PROPERTIES IMPORTED_LOCATION ${ramses-shared-lib-client-only_LIBRARIES})
        if (MSVC)
            set_target_properties(ramses-ramses-shared-lib-client-only PROPERTIES IMPORTED_IMPLIB ${ramses-shared-lib-client-only_LIBRARIES})
        endif()
        target_include_directories(ramses-ramses-shared-lib-client-only INTERFACE ${ramses-shared-lib-client-only_INCLUDE_DIRS})
        target_link_libraries(ramses-ramses-shared-lib-client-only INTERFACE glm::glm)

        if (NOT TARGET ramses::client-only)
            add_library(ramses::client-only ALIAS ramses-ramses-shared-lib-client-only)
            message(STATUS "Created ramses-shared-lib-client-only import target ramses::client-only")
        endif()
    endif()
endif()
